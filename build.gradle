buildscript {
    apply from: 'https://raw.githubusercontent.com/FINTprosjektet/fint-buildscripts/v1.0.0/dependencies.gradle'
    ext {
        springBootVersion = springBootVersion
    }
    repositories {
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath('org.ajoberstar:gradle-git:1.5.1')
    }
}

apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'org.springframework.boot'
apply plugin: 'maven'
apply plugin: 'jacoco'

import org.ajoberstar.grgit.Grgit
jar {
    version = Grgit.open().head().id.take(7)
    baseName = 'fint-provider'
}

import org.apache.tools.ant.filters.ReplaceTokens
afterEvaluate {
    configure(processResources) {
        filesMatching('**/*.yml') {
            filter(ReplaceTokens, tokens: [version: jar.version])
        }
    }
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    maven {
        url "http://dl.bintray.com/fint/maven"
    }
    mavenLocal()
    jcenter()
    mavenCentral()
}


dependencies {
    compileOnly('org.projectlombok:lombok')

    compile('no.fint:fint-audit-mongo-plugin:0.0.12')
    compile('no.fint:fint-events:0.0.25')
    compile('com.kohlschutter.junixsocket:junixsocket-common:2.0.4')
    compile('com.kohlschutter.junixsocket:junixsocket-native-common:2.0.4')

    compile('no.fint:fint-event-model:0.0.11')
    compile("com.github.springfox.loader:springfox-loader:${springfoxLoaderVersion}")
    compile('redis.clients:jedis:2.9.0')
    compile('org.springframework.data:spring-data-redis:1.7.5.RELEASE')
    compile('com.github.kstyrc:embedded-redis:0.6')
    compile('com.github.fakemongo:fongo:2.0.9')

    compile('org.springframework.boot:spring-boot-starter-amqp')
    compile('org.springframework.boot:spring-boot-starter-web')
    compile('org.springframework.cloud:spring-cloud-starter-config')
    runtime('org.springframework.boot:spring-boot-devtools')

    testCompile('cglib:cglib-nodep:3.2.4')
    testCompile("org.spockframework:spock-spring:${spockSpringVersion}")
    testCompile('org.springframework.boot:spring-boot-starter-test')
    testCompile('com.github.kstyrc:embedded-redis:0.6')
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:Camden.SR5"
    }
}

bootRun {
    systemProperties System.properties
}

jacocoTestReport {
    reports {
        xml.enabled true
    }
}
